#==================================================================================================================================
# This example shows how Cell names (devices' names to be precise) can be used inside other Cell's formulas.
# It also shows the use of 'mod()' and 'format()' functions.
#
# IMPORTANT: refer to 51.driver_cell_basic.une to better understand this example.
#
# ---------------------------------------------------------------------------------------------------------------------------------
# This file is part of the Mingle project, which is licensed under Apache 2.0 License
#
# @author : Francisco JosÃ© Morero Peyrona
# @project: https://mingle.peyrona.com
#==================================================================================================================================

DEVICE console
    DRIVER OutputDriver


DEVICE clock
    DRIVER ClockDriver
        CONFIG
            interval SET 1s

# Every second the clock changes its value and the device 'seconds' is automatically updated because
# its formula refers to 'clock'.

DEVICE seconds
     DRIVER CellSetDriver
        CONFIG
            value SET "= (clock / 1000):int()"

# And because the device 'seconds' changed, the device 'time_is' (and because its fomular refders to
# the device 'seconds') will also be updated.

DEVICE time_is
     DRIVER CellSetDriver
        CONFIG
            value SET "= (seconds / 3600):floor():mod(60):format('00') +':'+ \
                         (seconds /   60):floor():mod(60):format('00') +':'+ \
                                  seconds:floor():mod(60):format('00')"
            # Note the use of single quotes ('00') instead of double ("00") inside the formula

WHEN time_is:len() > 0       # This is always true
THEN console SET "Uptime: " + time_is

# The following rule is just to show that there is a shorter way to achieve same results.

WHEN clock ABOVE 0
THEN console SET "------> " + time( clock / 1000 )

# >>>>>>>>>>>>>> EOF <<<<<<<<<<<<<<<<