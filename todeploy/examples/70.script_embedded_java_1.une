#==================================================================================================================================
# This example shows how to use other language to perform small tasks (scripts).
# This is called "embedded" (AKA 'inline') code, and allows to mix Une code with other languages (JavScript, Python, Java, etc).
#
# Although there is no limitation to the size of embedded source code, long pieces should not be mixed with Une source code: just
# for clarity sake.
#
# Each SCRIPT is executed by ExEn in its own thread. Therefore execution order is not guaranteed.
# For more information, pleaser refer to the "Mingle Standard Platform" handbook.
#
# Note 1: intentionally, this example does not INCLUDE the normal "standard-replaces.une" (it is not needed).
# Note 2: as the example does not include a Rule, the Scripts will be never executed.
#
# ---------------------------------------------------------------------------------------------------------------------------------
# This file is part of the Mingle project. the Mingle project is licensed under Apache 2.0 License
#
# @author : Francisco JosÃ© Morero Peyrona
# @project: https://mingle.peyrona.com
#==================================================================================================================================

SCRIPT                                          # An example with only one method
   ONSTART
   LANGUAGE Java
   CALL "myJavaHello"                           # Method to be invoked (name is in between quotes, therefore it is case-sensitive)
   FROM {
            public void myJavaHello()                                             // Referred CALL method must be 'public void'
            {
                System.out.println( "Hello Mingle world from Java -> myJavaHello()\n" +
                                    "Time is: " + java.time.LocalTime.now() );    // It is mandatory to use fully qualified names
            }
        }


SCRIPT                                           # An example with no method (one will be generated internally)
   ONSTART
   LANGUAGE Java                                 # CALL clause is not needed
   FROM {
           System.out.println( "Hello Mingle world from Java -> anonimous\n" +
                               "Time is: " + java.time.LocalTime.now() );
        }


SCRIPT                                           # An example with more than one method
   ONSTART
   LANGUAGE java
   CALL "myJavaInspector"
   FROM {
            public void myJavaInspector( IRuntime rt )               // ExEn will inject this reference to IRuntime
            {
                for( Object script : rt.all( "scripts" ) )
                {
                    System.out.println( "There is an script named '" + ((IScript) script).name() +'\'' );
                }

                exit( rt );                                          // Other functions can be invoked
            }

            private void exit( IRuntime rt )
            {
                System.out.println( "Task accomplished" );
                rt.exit( 3000 );                                     // Ends ExEn execution in 3 seconds
            }                                                        // (used to exit abruptly, it is rarely used)
        }

# >>>>>>>>>>>>>> EOF <<<<<<<<<<<<<<<<