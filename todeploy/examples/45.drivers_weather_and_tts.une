#==================================================================================================================================
# This example shows how to use of open-meteo driver to retrieve current weather information and send it to FreeTTS to speak
# retrieved weather information.
#
# ---------------------------------------------------------------------------------------------------------------------------------
# This file is part of the Mingle project, which is licensed under Apache 2.0 License
#
# @author : Francisco JosÃ© Morero Peyrona
# @project: https://mingle.peyrona.com
#==================================================================================================================================

WHEN weather:len() > 0           # Weather is updated evey hour, but we want to send the info to the TTS every 1 minute, so we
THEN weather_info SET weather    # store the weather updates (hourly) into a Cell and read this Cell (using a clock) every minute.


WHEN ((clock > 0) OR (weather_info:len() > 0)) \   # We send info to TTS every time the clock or the weather info are updated.
     AND weather_info:type() IS "pair"             # But only if weather_info was already updated (depends on WebService response).
THEN TTS SET "Weather now is "+ weather_codes:get( weather_info:get( "code" ) ) + ".  " + \
             "Current temperature is "+ weather_info:get( "temperature" ) + " Celsius degrees.  "+ \
             "Currently the precipitation probability is "+ weather_info:get( "precipitation" ):format( "##" ) +" percent"

#----------------------------------------------------------------------------------------------------------------------------------

DEVICE weather_codes
    DRIVER CellSetDriver
        CONFIG
            value SET pair():put(  0, "clear sky"                     ) \
                            :put(  1, "mainly clear"                  ) \
                            :put(  2, "partly cloudy"                 ) \
                            :put(  3, "overcast"                      ) \
                            :put( 45, "fog"                           ) \
                            :put( 48, "depositing rime fog"           ) \
                            :put( 51, "light drizzle"                 ) \
                            :put( 53, "moderate drizzle"              ) \
                            :put( 55, "dense  drizzle"                ) \
                            :put( 56, "light freezing drizzle"        ) \
                            :put( 57, "dense freezing drizzle"        ) \
                            :put( 61, "slight rain"                   ) \
                            :put( 63, "moderate rain"                 ) \
                            :put( 65, "heavy rain"                    ) \
                            :put( 66, "light freezing rain"           ) \
                            :put( 67, "heavy freezing rain"           ) \
                            :put( 71, "slight snow"                   ) \
                            :put( 73, "moderate snow"                 ) \
                            :put( 75, "heavy snow"                    ) \
                            :put( 77, "snow grains"                   ) \
                            :put( 80, "slight rain showers"           ) \
                            :put( 81, "moderate rain showers"         ) \
                            :put( 82, "violent rain showers"          ) \
                            :put( 85, "slight snow showers"           ) \
                            :put( 86, "heavy snow showers"            ) \
                            :put( 95, "thunderstorm"                  ) \
                            :put( 96, "thunderstorm with slight hail" ) \
                            :put( 99, "thunderstorm with heavy hail"  )

DEVICE weather_info
    DRIVER CellSetDriver
        CONFIG
            value SET ""

DEVICE weather
    DRIVER OpenMeteoDriver
        CONFIG
            Latitude  SET 36.5149
            Longitude SET -4.8838

DEVICE TTS
    DRIVER SunFreeTTSDriver

DEVICE clock
    DRIVER ClockDriver
        CONFIG
            interval SET 1m

# >>>>>>>>>>>>>> EOF <<<<<<<<<<<<<<<<