#==================================================================================================================================
# This script shows basic handling for Une Extended Data Types: date, time, list, pair.
# It also shows how to use the ExEn internal cache.
#
# For a detailed functionality, please refer to "The Une language" book.
#
# ---------------------------------------------------------------------------------------------------------------------------------
# This file is part of the Mingle project, which is licensed under Apache 2.0 License
#
# @author : Francisco JosÃ© Morero Peyrona
# @project: https://github.com/peyrona/mingle
#==================================================================================================================================

SCRIPT
    ONSTART
    LANGUAGE Une
    FROM {
             # Using the internal cache (refer to "The Une language" book)

             put( "cpu" , list() )    # A list of CPU percentage usages
             put( "ram" , list() )    # A list of items
             put( "both", pair() )    # A dictionary (list of pairs: [key, value])

             console = "Wait until collections are populated..."
         }

DEVICE CPU
    DRIVER SystemMonitorDriver
        CONFIG
            metric   = "cpu"
            interval = 2s

DEVICE RAM
    DRIVER SystemMonitorDriver
        CONFIG
            metric   = "jvmram"
            interval = 4s

DEVICE console
    DRIVER OutputDriver

WHEN cpu >= 0
THEN get("cpu" ):add( cpu )                  # Cache changes are not DEVICE changes and therefore RULEs will be not re-evaluated.
     get("both"):put( time() +" CPU", cpu )

WHEN ram >= 0
THEN get("ram" ):add( ram:format("##") )
     get("both"):put( time() +" RAM", ram:format("##") )

WHEN cpu >= 0 AND get("cpu"):size() == 5
THEN console = "{"+ time() +"} CPU "+ get("cpu") + _NL_

WHEN ram >= 0 AND get("ram"):size() == 5
THEN console = "{"+ time() +"} RAM "+ get("ram") + _NL_

WHEN cpu >= 0 AND get("cpu"):size() > 5
THEN get("cpu"):empty()

WHEN ram >= 0 AND get("ram"):size() > 5
THEN get("ram"):empty()

WHEN (cpu >= 0 OR ram >= 0) AND get("both"):size() == 12
THEN console = "BOTH --> "+ get("both") + _NL_

WHEN (cpu >= 0 OR ram >= 0) AND get("both"):size() > 12
THEN get("both"):empty()

# >>>>>>>>>>>>>> EOF <<<<<<<<<<<<<<<<