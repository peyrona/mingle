#==================================================================================================================================
# This example is similar introduces the following functions:
#    * getTriggerName()   --> Returns the name  of the device that triggered the evaluation of the RULE's WHEN.
#    * getTriggerValue()  --> Returns the value of the device that triggered the evaluation of the RULE's WHEN.
#
# These functions are useful in scenarios like the one in file "07.cpu_ram_disk_groups.une", we had following RULE:
#
# WHEN ANY metric ABOVE 80
# THEN outputs SET "["+ date() +" "+ time() +"] Care! At least one metric is above 80% (using ANY)"
#
# The THEN tells that 'At least one metric is above 80%', but it does not say which one is: CPU or RAM or Disk is above 80%
#
# ---------------------------------------------------------------------------------------------------------------------------------
# This file is part of the Mingle project, which is licensed under Apache 2.0 License
#
# @author : Francisco JosÃ© Morero Peyrona
# @project: https://github.com/peyrona/mingle
#==================================================================================================================================

SCRIPT
	ONSTART
	LANGUAGE une
	FROM {
            console = "Wait until randomly the three doors are open (their value is 'false')"
         }


# A device value of 'false' means 'open' and a value of 'true' means 'closed' (this is the normal meaninmg in electronic)

WHEN ALL doors ARE OPEN
THEN console SET "The last door to be open was: '"+ getTriggeredBy():get("name") +"'"

#----------------------------------------------------------------------------------------------------------------------------------

DEVICE main
    INIT
        groups SET "doors"
    DRIVER CellSetDriver
        CONFIG
            value SET closed

DEVICE back
    INIT
        groups SET "doors"
    DRIVER CellSetDriver
        CONFIG
            value SET closed

DEVICE garage
    INIT
        groups SET "doors"
    DRIVER CellSetDriver
        CONFIG
            value SET closed

DEVICE clock                       # Used to generate random values at especific interval: every 3 seconds
    DRIVER ClockDriver
        CONFIG
            interval SET 2s

DEVICE console
    DRIVER OutputDriver

WHEN clock > 0
THEN main   SET iif( rand( 0, 2 ) > 1, open, closed )
     back   SET iif( rand( 0, 2 ) > 1, open, closed )
     garage SET iif( rand( 0, 2 ) > 1, open, closed )

# >>>>>>>>>>>>>> EOF <<<<<<<<<<<<<<<<